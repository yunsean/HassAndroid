// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext.kotlin_version = '1.3.21'
    ext {
        shared_library_version = "v1.0.7.3"
        support_lib_version = "28.0.0"
        anko_version = '0.10.7'
        compileSdkVersion = 28
        buildToolsVersion = "28.0.3"
        minSdkVersion = 22
        targetSdkVersion = 23
        androidGradleVersion = '2.3.1'
        versionCode = 13
        versionName = "1.0"
    }
    repositories {
        google()
        jcenter()
        maven { url "https://jcenter.bintray.com" }
        maven { url 'https://jitpack.io' }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.4.0'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-android-extensions:$kotlin_version"
        classpath 'com.github.dcendents:android-maven-gradle-plugin:2.1'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.2'
    }
}

allprojects {
    repositories {
        google()
        jcenter()
        maven { url "https://jcenter.bintray.com" }
        maven { url 'https://jitpack.io' }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

subprojects {
    project.configurations.all {
        resolutionStrategy.eachDependency { details ->
            if (details.requested.group == 'com.android.support' && !details.requested.name.contains('multidex')) {
                details.useVersion "$support_lib_version"
            }
            if (details.requested.group == 'org.jetbrains.anko') {
                details.useVersion "$anko_version"
            }
            if (details.requested.group == 'org.jetbrains.kotlin') {
                details.useVersion "$kotlin_version"
            }
            if (details.requested.group == 'com.github.yunsean.SharedLibrary') {
                details.useVersion "$shared_library_version"
            }
        }
    }
}
